###################################################
#                                                 #
# Written by Spyros Voulgaris (voulgaris@aueb.gr) #
# April 2021                                      #
#                                                 #
# Mobile Multimedia Lab                           #
# Athens University of Economics and Business     #
# Athens, Greece                                  #
#                                                 #
###################################################

#set term png size 1920,1200
#set term png size 1600,900
#set terminal pdfcairo enhanced size 32cm,18cm font ",8"
#set terminal pdfcairo enhanced font ",8"
#set term postscript eps enhanced color 13
#set size 1.6,1.2
#set size 2,2
set term qt

min(a,b) = a<b ? a : b
max(a,b) = a>b ? a : b
max3(a,b,c) = max(max(a,b),max(a,c))

set key opaque

set xlabel "time (sec)"

set xrange [0:*]
set yrange [] reverse
set xtics 1
set ytics 1
set grid

plot "DATA" index 0 using 2:1:($3-$2):(0) with vectors nohead lw 2 lc 4 title "dial", \
     "DATA" index 0 using 4:1 with points pt 2 ps 1.5 lw 2 lc 7 title "dial error", \
     "DATA" index 0 using (min($5,$6)):1:($6-$5):(0) with vectors nohead lw 6 lc 3 title "query", \
     "DATA" index 0 using (min($5,$7)):1:($7-$5):(0):5 with vectors nohead lw 6 lc rgb '#A056B4E9' title "unfinished query", \
     "DATA" index 0 using 2:1 with points pt 7 ps 0.6 lc 4 notitle, \
     "DATA" index 1 using 1:2:($3-$1):($4-$2) with vectors filled lc '#20C7162B' lw 1.5 dt 2 title "causality", \
     "DATA" index 1 using 1:2 with points lc '#20C7162B' pt 7 ps 0.6 notitle, \
     "DATA" index 0 using (max3($3,$6,$7)):1:(sprintf("  %s",stringcolumn(8))):($9==0?8:2) with labels textcolor variable left notitle, \
     "DATA" index 2 using 1:(0):(0):2 with vectors nohead lw 3 dt 4 lc 8 title "context canceled", \
     "DATA" index 3 using 1:(0):(0):2 with vectors nohead lw 3 dt 4 lc 2 title "results finalized"

pause -1
